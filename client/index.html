<!DOCTYPE html>
<html lang="en">
  <head>
    <title>OpenAI Chat API</title>
    <base href="." />
    <link rel="icon" type="image/png" href="images/favicon.ico" />
    <link rel="stylesheet" href="style.css" />
    <link
      rel="stylesheet"
      href="https://cdnjs.cloudflare.com/ajax/libs/animate.css/4.1.1/animate.min.css"
    />
    <link
      href="https://cdn.jsdelivr.net/npm/bootstrap@5.3.0-alpha2/dist/css/bootstrap.min.css"
      rel="stylesheet"
      integrity="sha384-aFq/bzH65dt+w6FI2ooMVUpc+21e0SRygnTpmBvdBgSdnuTN7QbdgL+OapgHtvPp"
      crossorigin="anonymous"
    />
  </head>
  <body>
    <canvas class="webgl"></canvas>
    <script id="vertex-shader" type="no-js">
      void main()	{
        gl_Position = vec4( position, 1.0 );
      }
    </script>
    <script id="fragment-shader" type="no-js">
      uniform float iGlobalTime;
      uniform vec2 iResolution;

      const int NUM_STEPS = 8;
      const float PI	 	= 3.1415;
      const float EPSILON	= 1e-3;
      float EPSILON_NRM	= 0.1 / iResolution.x;

      // sea variables
      const int ITER_GEOMETRY = 3;
      const int ITER_FRAGMENT = 5;
      const float SEA_HEIGHT = 0.6;
      const float SEA_CHOPPY = 1.0;
      const float SEA_SPEED = 1.0;
      const float SEA_FREQ = 0.16;
      const vec3 SEA_BASE = vec3(0.1,0.19,0.22);
      const vec3 SEA_WATER_COLOR = vec3(0.5,0.8,0.95);
      float SEA_TIME = iGlobalTime * SEA_SPEED;
      mat2 octave_m = mat2(1.6,1.2,-1.2,1.6);

      mat3 fromEuler(vec3 ang) {
        vec2 a1 = vec2(sin(ang.x),cos(ang.x));
        vec2 a2 = vec2(sin(ang.y),cos(ang.y));
        vec2 a3 = vec2(sin(ang.z),cos(ang.z));
        mat3 m;
        m[0] = vec3(
          a1.y*a3.y+a1.x*a2.x*a3.x,
          a1.y*a2.x*a3.x+a3.y*a1.x,
          -a2.y*a3.x
        );
        m[1] = vec3(-a2.y*a1.x,a1.y*a2.y,a2.x);
        m[2] = vec3(
          a3.y*a1.x*a2.x+a1.y*a3.x,
          a1.x*a3.x-a1.y*a3.y*a2.x,
          a2.y*a3.y
        );
        return m;
      }

      float hash( vec2 p ) {
        float h = dot(p,vec2(127.1,311.7));
        return fract(sin(h)*43758.5453123);
      }

      float noise( in vec2 p ) {
        vec2 i = floor(p);
        vec2 f = fract(p);
        vec2 u = f * f * (3.0 - 2.0 * f);
        return -1.0 + 2.0 * mix(
          mix(
            hash(i + vec2(0.0,0.0)
          ),
          hash(i + vec2(1.0,0.0)), u.x),
          mix(hash(i + vec2(0.0,1.0) ),
          hash(i + vec2(1.0,1.0) ), u.x),
          u.y
        );
      }

      float diffuse(vec3 n,vec3 l,float p) {
        return pow(dot(n,l) * 0.4 + 0.6,p);
      }

      float specular(vec3 n,vec3 l,vec3 e,float s) {
        float nrm = (s + 8.0) / (3.1415 * 8.0);
        return pow(max(dot(reflect(e,n),l),0.0),s) * nrm;
      }

      vec3 getSkyColor(vec3 e) {
        e.y = max(e.y, 0.0);
        vec3 ret;
        ret.x = pow(1.0 - e.y, 2.0);
        ret.y = 1.0 - e.y;
        ret.z = 0.6+(1.0 - e.y) * 0.4;
        return ret;
      }


      float sea_octave(vec2 uv, float choppy) {
        uv += noise(uv);
        vec2 wv = 1.0 - abs(sin(uv));
        vec2 swv = abs(cos(uv));
        wv = mix(wv, swv, wv);
        return pow(1.0 - pow(wv.x * wv.y, 0.65), choppy);
      }

      float map(vec3 p) {
        float freq = SEA_FREQ;
        float amp = SEA_HEIGHT;
        float choppy = SEA_CHOPPY;
        vec2 uv = p.xz;
        uv.x *= 0.75;

        float d, h = 0.0;
        for(int i = 0; i < ITER_GEOMETRY; i++) {
          d = sea_octave((uv + SEA_TIME) * freq, choppy);
          d += sea_octave((uv - SEA_TIME) * freq, choppy);
          h += d * amp;
          uv *= octave_m;
          freq *= 1.9;
          amp *= 0.22;
          choppy = mix(choppy, 1.0, 0.2);
        }
        return p.y - h;
      }

      float map_detailed(vec3 p) {
          float freq = SEA_FREQ;
          float amp = SEA_HEIGHT;
          float choppy = SEA_CHOPPY;
          vec2 uv = p.xz;
          uv.x *= 0.75;

          float d, h = 0.0;
          for(int i = 0; i < ITER_FRAGMENT; i++) {
            d = sea_octave((uv+SEA_TIME) * freq, choppy);
            d += sea_octave((uv-SEA_TIME) * freq, choppy);
            h += d * amp;
            uv *= octave_m;
            freq *= 1.9;
            amp *= 0.22;
            choppy = mix(choppy,1.0,0.2);
          }
          return p.y - h;
      }

      vec3 getSeaColor(
        vec3 p,
        vec3 n,
        vec3 l,
        vec3 eye,
        vec3 dist
      ) {
        float fresnel = 1.0 - max(dot(n,-eye),0.0);
        fresnel = pow(fresnel,3.0) * 0.65;

        vec3 reflected = getSkyColor(reflect(eye,n));
        vec3 refracted = SEA_BASE + diffuse(n,l,80.0) * SEA_WATER_COLOR * 0.12;

        vec3 color = mix(refracted,reflected,fresnel);

        float atten = max(1.0 - dot(dist,dist) * 0.001, 0.0);
        color += SEA_WATER_COLOR * (p.y - SEA_HEIGHT) * 0.18 * atten;

        color += vec3(specular(n,l,eye,60.0));

        return color;
      }

      // tracing
      vec3 getNormal(vec3 p, float eps) {
        vec3 n;
        n.y = map_detailed(p);
        n.x = map_detailed(vec3(p.x+eps,p.y,p.z)) - n.y;
        n.z = map_detailed(vec3(p.x,p.y,p.z+eps)) - n.y;
        n.y = eps;
        return normalize(n);
      }

      float heightMapTracing(vec3 ori, vec3 dir, out vec3 p) {
        float tm = 0.0;
        float tx = 1000.0;
        float hx = map(ori + dir * tx);

        if(hx > 0.0) {
          return tx;
        }

        float hm = map(ori + dir * tm);
        float tmid = 0.0;
        for(int i = 0; i < NUM_STEPS; i++) {
          tmid = mix(tm,tx, hm/(hm-hx));
          p = ori + dir * tmid;
          float hmid = map(p);
          if(hmid < 0.0) {
            tx = tmid;
            hx = hmid;
          } else {
            tm = tmid;
            hm = hmid;
           }
        }
        return tmid;
      }

      void main() {
        vec2 uv = gl_FragCoord.xy / iResolution.xy;
        uv = uv * 2.0 - 1.0;
        uv.x *= iResolution.x / iResolution.y;
        float time = iGlobalTime * 0.3;

        // ray
        vec3 ang = vec3(
          sin(time*3.0)*0.1,sin(time)*0.2+0.3,time
        );
        vec3 ori = vec3(0.0,3.5,time*5.0);
        vec3 dir = normalize(
          vec3(uv.xy,-2.0)
        );
        dir.z += length(uv) * 0.15;
        dir = normalize(dir);

        // tracing
        vec3 p;
        heightMapTracing(ori,dir,p);
        vec3 dist = p - ori;
        vec3 n = getNormal(
          p,
          dot(dist,dist) * EPSILON_NRM
        );
        vec3 light = normalize(vec3(0.0,1.0,0.8));

        // color
        vec3 color = mix(
          getSkyColor(dir),
          getSeaColor(p,n,light,dir,dist),
          pow(smoothstep(0.0,-0.05,dir.y),0.3)
        );

        // post
        gl_FragColor = vec4(pow(color,vec3(0.75)), 1.0);
      }
    </script>
    <div id="root">
      <header>
        <nav class="navbar bg-dark" data-bs-theme="dark">
          <div class="nav-bar-brand">
            <h3>Budget Company</h3>
          </div>
          <ul class="nav-bar-menu">
            <li><a href="#">Home</a></li>
            <li><a href="#">About</a></li>
            <li><a href="#">News</a></li>
            <li><a href="#">Contact</a></li>
          </ul>
        </nav>
      </header>
      <main>
        <div id="container">
          <h1 class="main-text animate__animated animate__lightSpeedInRight">
            Please talk to our Chat Support Bot
          </h1>
          <h2
            class="second-text animate__animated animate__fadeInDown animate__delay-1s"
          >
            Because we are saving money on hiring support personnel
          </h2>
          <h3
            class="third-text animate__animated animate__fadeInBottomRight animate__delay-2s"
          >
            Do enjoy this ocean view!
          </h3>
        </div>
        <div class="chat-container">
          <div class="chat-header bg-dark" data-bs-theme="dark">
            <h5>Customer Support</h5>
            <!-- <button class="close-button">&times;</button> -->
            <button class="maximize-button">&#128470;</button>
          </div>
          <section>
            <ul id="dialogue" class="dialogue">
              <li>Hi there ðŸ‘‹ I can help you with your support questions!</li>
            </ul>
          </section>
          <section class="form-section">
            <!-- <form id="prompt-form">
              <input
                id="prompt-input"
                type="text"
                name="prompt"
                placeholder="Type your message here..."
              />
              <button class="send-button" type="submit">&#9658;</button>
            </form> -->
            <form class="input-group mb-3" id="prompt-form">
              <input
                id="prompt-input"
                type="text"
                name="prompt"
                class="form-control"
                placeholder="Type your message here..."
                aria-label="input text"
                aria-describedby="button-addon2"
              />
              <button
                class="btn btn-outline-secondary bg-black"
                data-bs-theme="dark"
                type="submit"
                id="button-addon2"
              >
                Send
              </button>
            </form>
          </section>
        </div>
      </main>
      <footer class="copy-right bg-dark" data-bs-theme="dark">
        <div>
          <p>&copy; 2023 AI Team. All rights reserved.</p>
        </div>
      </footer>
    </div>
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/3.6.3/jquery.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r77/three.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/three@0.132.2/examples/js/controls/OrbitControls.js"></script>
    <script src="app/ocean.js" type="module"></script>
    <script src="app/index.js" type="module"></script>
  </body>
</html>
